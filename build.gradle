repositories {
    mavenCentral();
}
configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all {resolutionStrategy.cacheChangingModulesFor 30, 'minutes'}
}
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing' //使用signing plugin做数字签名

//定义GroupID和Version，ArtefactID会自动使用Project名
    group = 'org.xforth'
    version = '0.2.0-SNAPSHOT'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    dependencies {
        /* spring */
        compile "org.springframework:spring-context-support:3.2.9.RELEASE"

        /* apache commons ,google commons*/
        compile 'org.apache.commons:commons-lang3:3.3.2'

        /* log */
        compile "org.slf4j:jcl-over-slf4j:1.7.7"
        compile "ch.qos.logback:logback-classic:1.1.2"
        compile("org.logback-extensions:logback-ext-spring:0.1.2") { transitive = false }

        /* others */
        compile "org.apache.curator:curator-recipes:2.4.2"
        compile 'javax.transaction:jta:1.1'
        compile 'org.aspectj:aspectjrt:1.8.1'
        compile 'org.aspectj:aspectjweaver:1.8.1'

        /* test */
        testCompile 'junit:junit:4.11'
        testCompile "org.springframework:spring-test:3.2.9.RELEASE"
    }
    idea {
        module {
            excludeDirs = []
            [".idea", ".gradle", '.settings'].each {
                excludeDirs << file("$it")
            }
        }
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                //为Pom文件做数字签名
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                //指定项目部署到的中央库地址，UserName和Password就是Part 1中注册的账号。
                repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: "XXX", password: "YYY")
                }
                //构造项目的Pom文件，参见Part 2中Pom文件的规范，不要遗漏必填项
                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Toggle is a little java tool to make your life easier when you try to use Feature Toggle in Java.'
                    url 'https://github.com/xforth/jforth-config'

                    scm {
                        url 'scm:git@github.com:xforth/jforth-config.git'
                        connection 'scm:git@github.com:xforth/jforth-config.git'
                        developerConnection 'git@github.com:xforth/jforth-config.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://mit-license.org/'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'vernonzheng'
                            name 'Vernon Zheng'
                        }
                    }
                }
            }
        }
    }


//参见Part 2， 为项目生成**.jar/**-javadoc.jar/**-sources.jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

//为所有的jar包做数字签名
    signing {
        sign configurations.archives
    }
}


